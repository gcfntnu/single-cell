#-*- mode:snakemake -*-
from os.path import join
import string

# config
extra_conf_fn = srcdir('ensembl.config')
if os.path.exists(extra_conf_fn):
    with open(extra_conf_fn) as fh:
        c  = yaml.load(fh) or {}
        update_config2(config, c)

ENSEMBL_EXT = join(EXT_DIR, 'ensembl')
ORG = config.get('organism', 'homo_sapiens')
DB_CONF = config.get('ensembl', {})
ASSEMBLY = DB_CONF.get('assembly', 'primary_assembly')
ENS_RELEASE = DB_CONF.get('ensembl_release', '92')
ENS_VER = DB_CONF.get('ensembl_version', 'GRCh38')
if ORG == 'homo_sapiens':
    ENS_NAME = '' if ENS_VER == 'GRCh38' else '/grch37'
else:
    ENS_NAME = ''
TRANSCRIPTOME_TYPE = DB_CONF.get('transcriptome_type') #['prebuilt', 'all', 'chr', 'pri']
PROTOCOL = DB_CONF.get('protocol', 'http')
ERCC = DB_CONF.get('add_ercc', False)

# includes
include:
    'ercc.db'

# ENSEMBL URLS 
if ASSEMBLY == 'primary_assembly' or ASSEMBLY == 'primary':
    GENOME = '{}://ftp.ensembl.org/pub{}/release-{}/fasta/{}/dna/{}.{}.dna.primary_assembly.fa.gz'
    GENOME = GENOME.format(PROTOCOL, ENS_NAME, ENS_RELEASE, ORG, string.capwords(ORG), ENS_VER)
    GTF = '{}://ftp.ensembl.org/pub{}/release-{}/gtf/{}/{}.{}.{}.gtf.gz'
    GTF = GTF.format(PROTOCOL, ENS_NAME, ENS_RELEASE, ORG, string.capwords(ORG), ENS_VER, ENS_RELEASE)
elif ASSEMBLY == 'all':
    GENOME = '{}://ftp.ensembl.org/pub{}/release-{}/fasta/{}/dna/{}.{}.dna.toplevel.fa.gz'
    GENOME = GENOME.format(ROTOCOL, ENS_NAME, ENS_RELEASE, ORG, string.capwords(ORG), ENS_VER)
    GTF = '{}://ftp.ensembl.org/pub{}/release-{}/gtf/{}/{}.{}.{}.chr_patch_hapl_scaff.gtf.gz'
    GTF = GTF.format(PROTOCOL, ENS_NAME, ENS_RELEASE, ORG, string.capwords(ORG), ENS_VER, ENS_RELEASE)
elif ASSEMBLY == 'chr':
    GENOME = '{}://ftp.ensembl.org/pub{}/release-{}/gtf/{}/{}.{}.{}.chr.gtf.gz'
    GENOME = GENOME.format(ROTOCOL, ENS_NAME, ENS_RELEASE, ORG, string.capwords(ORG), ENS_VER, ENS_RELEASE)
    GTF = '{}://ftp.ensembl.org/pub{}/release-{}/gtf/{}/{}.{}.{}.chr.gtf.gz'
    GTF = GTF.format(PROTOCOL, ENS_NAME, ENS_RELEASE, ORG, string.capwords(ORG), ENS_VER, ENS_RELEASE)
else:
    raise ValueError('ASSEMBLY option needs to be one of : primary, all, chr')

ENS_TRANSCRIPTOME = '{}://ftp.ensembl.org/pub{}/release-{}/fasta/{}/cdna/{}.{}.cdna.all.fa.gz'
ENS_TRANSCRIPTOME = ENS_TRANSCRIPTOME.format(PROTOCOL, ENS_NAME, ENS_RELEASE, ORG, string.capwords(ORG), ENS_VER)
ENS_NONCODING = '{}://ftp.ensembl.org/pub{}/release-{}/fasta/{}/ncrna/{}.{}.ncrna.fa.gz'
ENS_NONCODING = ENS_NONCODING.format(PROTOCOL, ENS_NAME, ENS_RELEASE, ORG, string.capwords(ORG), ENS_VER)

rule ensembl_dna:
    output:
        join(ENSEMBL_EXT, GENOME.split('/')[-1].split('.gz')[0])
    params: 
        url = GENOME
    threads: 
        32
    log:
        'logs/ensembl.genome.log'
    shell: 
        'wget -O - {params.url} -o {log} | gunzip -c > {output}'

rule ensembl_cdna:
    params: 
        url = ENS_TRANSCRIPTOME
    output:
        join(ENSEMBL_EXT, ENS_TRANSCRIPTOME.split('/')[-1].split('.gz')[0])
    threads: 
        32
    log:
        'logs/ensembl.cdna.log'
    shell: 
        'wget -O - {params.url} -o {log} | gunzip -c > {output}'

rule ensembl_noncoding:
    params:
        url = ENS_NONCODING
    output:
        join(ENSEMBL_EXT, ENS_NONCODING.split('/')[-1].split('.gz')[0])
    threads:
        32
    log:
        'logs/ensembl.noncoding.log'
    shell: 
        'wget -O - {params.url} -o {log} | gunzip -c > {output}'
        
rule ensembl_gtf:
    params: 
        url = GTF
    output: 
        join(ENSEMBL_EXT, GTF.split('/')[-1].split('.gz')[0])    
    threads:
        32
    log:
        'logs/ensembl.gtf.log'
    shell: 
        'wget -O - {params.url} -o {log} | gunzip -c > {output}'

rule ensembl_gtf_extract_biotypes:
    input:
        rules.ensembl_gtf.output
    output:
        join(ENSEMBL_EXT,'gtf_gene_biotypes.tsv')
    threads:
        1
    shell:
        'awk \'BEGIN {{printf "gene_id\\tgene_biotype\\n"}} $3 == "gene" {{for(i=1;i<=NF;i++)if($i=="gene_biotype") print substr($10,1,length($10)-1) "\\t" substr($(i+1),1,length($(i+1))-1) }}\' {input} > {output}'


rule ensembl_addercc2genome:
    input:
        rules.ensembl_dna.output,
        rules.ercc_files.output.ercc_fasta
    output:
        rules.ensembl_dna.output[0].split('.fa')[0] + '.ERCC92.fa'
    shell:
        'cat {input} > {output}'

rule ensembl_add_ercc2cdna:
    input:
        rules.ensembl_cdna.output,
        rules.ercc_files.output.ercc_fasta
    output:
        rules.ensembl_cdna.output[0].split('.fa')[0] + '.ERCC92.fa'
    shell:
        'cat {input} > {output}'

rule ensembl_add_ercc2noncoding:
    input:
        rules.ensembl_noncoding.output,
        rules.ercc_files.output.ercc_fasta
    output:
        rules.ensembl_noncoding.output[0].split('.fa')[0] + '.ERCC92.fa'
    shell:
        'cat {input} > {output}'

rule ensembl_add_ercc2gtf:
    input:
        rules.ensembl_gtf.output,
        rules.ercc_gtf.output
    output:
        rules.ensembl_gtf.output[0].split('.gtf')[0] + '.ERCC92.gtf'
    shell:
        'cat {input} > {output}'

rule ensembl_db_downloads:
    input:
        genome = rules.ensembl_add_ercc2genome.output if ERCC else rules.ensembl_dna.output,
        cdna = rules.ensembl_add_ercc2cdna.output if ERCC else rules.ensembl_cdna.output,
        noncoding = rules.ensembl_noncoding.output,
        gtf = rules.ensembl_add_ercc2gtf.output if ERCC else rules.ensembl_gtf.output,
    output:
        genome = join(ENSEMBL_EXT, 'fasta', 'genome.fa'),
        cdna = join(ENSEMBL_EXT, 'fasta', 'transcriptome.fa'),
        gtf = join(ENSEMBL_EXT, 'genes', 'genes.gtf'),
        noncoding = join(ENSEMBL_EXT, 'fasta', 'ncdna.fa')
    shell:
        """
        ln -sr {input.genome} {output.genome}
        ln -sr {input.cdna} {output.cdna}
        ln -sr {input.gtf} {output.gtf}
        ln -sr {input.noncoding} {output.noncoding}
        """

rule ensembl_index_genome:
    input:
        join(ENSEMBL_EXT, 'fasta', 'genome.fa')
    output:
        join(ENSEMBL_EXT, 'fasta', 'genome.fai')
    singularity:
        'docker://gcfntnu/ensembl:0.1'
    shell:
        'samtools index {input}'
        
rule ensembl_sequence_dict:
    input:
        join(ENSEMBL_EXT, 'fasta', 'genome.fa')
    output:
        join(ENSEMBL_EXT, 'fasta', 'genome.dict')
    singularity:
        'docker://gcfntnu/ensembl:0.1'    
    shell:
        'picard CreateSequenceDictionary -Xms2g -Xmx2g R={input} O={output}'

rule ensembl_star_genome_index:
    input: 
        genome = join(ENSEMBL_EXT, 'fasta', 'genome.fa'),
        gtf = join(ENSEMBL_EXT, 'genes', 'genes.gtf')
    output:
         join(ENSEMBL_EXT, 'star', 'SA')
    params:
        index_dir =  join(ENSEMBL_EXT, 'star'),
        readlength = '100'
    threads:
        48
    log:
        join(ENSEMBL_EXT, 'logs', 'STAR.index.log')
    singularity:
        'docker://gcfntnu/star:2.7.0d' 
    shell:
        'STAR '
        '--runThreadN {threads} '
        '--runMode genomeGenerate '
        '--genomeDir {params.index_dir} '
        '--genomeFastaFiles {input.genome} '
        '--sjdbGTFfile {input.gtf} '
        '--sjdbOverhang {params.readlength} '
        
rule ensembl_hisat2_genome_index:
    input:
        genome = join(ENSEMBL_EXT, 'fasta', 'genome.fa')
    params:
        index_dir =  join(ENSEMBL_EXT, 'hisat2')
    output:
         join(ENSEMBL_EXT, 'hisat2', 'genome.1.ht2')
    threads:
        24
    log:
        join(ENSEMBL_EXT, 'logs', 'HISAT2.index.log')
    singularity:
        'docker://gcfntnu/ensembl:0.1' 
    shell:
        'hisat2-build '
        '-f '
        '-p {threads} '
        '{input.genome} '
        '{params.index_dir}'

rule ensembl_bowtie2_genome_index:
    input:
        genome = join(ENSEMBL_EXT, 'fasta', 'genome.fa')
    params:
        index_dir =  join(ENSEMBL_EXT, 'bowtie2')
    output:
         join(ENSEMBL_EXT, 'bowtie2', 'genome.1.bt2')
    threads:
        24
    log:
        join(ENSEMBL_EXT, 'logs', 'BOWTIE2.index.log')
    singularity:
        'docker://gcfntnu/ensembl:0.1'   
    shell:
        'bowtie2-build --threads {threads} {input.genome} {params.index_dir}'

rule ensembl_bowtie_genome_index:
    input:
        genome = join(ENSEMBL_EXT, 'fasta', 'genome.fa')
    params:
        index_dir =  join(ENSEMBL_EXT, 'bowtie')
    output:
         join(ENSEMBL_EXT, 'bowtie', 'genome.1.ebwt')
    threads:
        24
    log:
        join(ENSEMBL_EXT, 'logs', 'BOWTIE.index.log')
    singularity:
        'docker://gcfntnu/ensembl:0.1'
    shell:
        'bowtie-build {input.genome} {params.index_dir}'
        
rule ensembl_novoalign_genome_index:
    input:
        genome = join(ENSEMBL_EXT, 'fasta', 'genome.fa')
    output:
         join(ENSEMBL_EXT, 'novoalign', 'genome.idx')
    threads:
        24
    log:
        join(ENSEMBL_EXT, 'logs', 'NOVOALIGN.index.log')
    singularity:
        'docker://gcfntnu/ensembl:0.1'
    shell:
        'novoindex -t {threads} {output} {input.genome}'
        
rule ensembl_kallisto_transcriptome_index:
    input:
        transcriptome = join(ENSEMBL_EXT, 'fasta', 'transcriptome.fa')
    output:
         join(ENSEMBL_EXT, 'kallisto', 'genome.idx')
    params:
        '-k 31'
    log:
        join(ENSEMBL_EXT, 'logs', 'KALLISTO.index.log')
    singularity:
        'docker://gcfntnu/ensembl:0.1'
    shell:
        'kallisto index '
        '--index={output} '
        '{params} '
        '{input.transcriptome} '
      

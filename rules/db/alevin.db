#-*- mode:snakemake -*-
AVN_EXT = join(EXT_DIR, 'alevin')
AVN_CONFIG = config.get('alevin', {})
AVN_DOCKER = AVN_CONFIG.get('docker', 'docker://flatberg/alevin:dev0.12')

rule alevin_transcriptome:
    input:
        gtf = join(EXT_DIR, '{reference_db}', 'genes', 'genes.gtf'),
        genome = join(EXT_DIR, '{reference_db}', 'fasta', 'genome.fa')
    output:
        join(EXT_DIR, '{reference_db}', 'fasta', 'transcriptome.fa')
    singularity:
        'docker://gcfntnu/alevin:0.13.1'
    shell:
        'gffread -w {output} -G -E -g {input.genome} {input.gtf}'
        
rule alevin_index:
    input:
        transcriptome = join(EXT_DIR, '{reference_db}', 'fasta', 'transcriptome.fa')
    output:
        join(EXT_DIR, '{reference_db}', 'salmon', 'refInfo.json')
    params:
        out = join(EXT_DIR, '{reference_db}', 'salmon')
    threads:
        16
    singularity:
        'docker://gcfntnu/alevin:0.13.1'
    shell:
        'salmon index '
        '--perfectHash '
        '--threads {threads} '
        '--index {params.out} '
        '--transcripts {input.transcriptome}'

rule ensembl_tx_csv:
    input:
       gtf = join(EXT_DIR, '{reference_db}', 'genes', 'genes.gtf')
    output:
        join(EXT_DIR, '{reference_db}', 'genes', 'transcripts.tsv')
    params:
        script = srcdir('scripts/gtf2tx.py')
    singularity:
        'docker://gcfntnu/alevin:0.13.1'
    shell:
        'python {params.script} {input} > {output}'
        
rule alevin_tgmap:
    input:
        gtf = join(EXT_DIR, '{reference_db}', 'genes', 'transcripts.tsv')
    output:
        join(EXT_DIR, '{reference_db}', 'genes', 'txp2gene.tsv')
    shell:
        'cut -f1,10  {input} > {output} '

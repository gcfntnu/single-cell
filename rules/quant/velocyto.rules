#-*- mode:snakemake -*-

extra_conf_fn = srcdir('velocyto.config')
if os.path.exists(extra_conf_fn):
    with open(extra_conf_fn) as fh:
        c  = yaml.load(fh) or {}
        update_config2(config, c)

#config
ORG = config.get('organism', 'homo_sapiens')
VELOCYTO_INTERIM = join(QUANT_INTERIM, 'velocyto')
VELOCYTO_CONF = config['quant'].get('velocyto', {})


include:
    '../gcfdb/ucsc.db'
include:
    'cellranger.rules'

rule velocyto_cellranger:
    input:
        rep_gtf = join(EXT_DIR, 'ucsc', ORG, 'genes', 'rmsk.gtf'),
        gtf = join(REF_DIR, 'genes', 'genes.gtf'),
        h5 = join(QUANT_INTERIM, 'cellranger', '{sample}', 'outs', 'filtered_feature_bc_matrix.h5')
    params:
        sample = join(QUANT_INTERIM, 'cellranger', '{sample}')
    output:
        join(QUANT_INTERIM, 'cellranger', '{sample}', 'velocyto', '{sample}.loom')
    singularity:
        'docker://gcfntnu/velocyto:0.17.17'
    shell:
        'velocyto run10x '
        '--samtools-threads 8 '
        '--verbose '
        '-m {input.rep_gtf} '
        '{params.sample} '
        '{input.gtf} '

rule velocyto_scanpy:
    input:
        join(QUANT_INTERIM, '{quant}', '{sample}', 'velocyto', '{sample}.loom')
    params:
        script = srcdir('scripts/convert_scanpy.py')
    output:
        join(QUANT_INTERIM, '{quant}', '{sample}', 'velocyto', '{sample}.h5ad')
    singularity:
        'docker://gcfntnu/scanpy:1.4.3'
    shell:
        'python {params.script} {input} -o {output} -v -f velocyto '

rule velocyto_aggr_scanpy:
    input:
        loom = expand(join(QUANT_INTERIM, 'cellranger', '{sample}', 'velocyto', '{sample}.loom'), sample=SAMPLES),
        csv = join(QUANT_INTERIM, 'aggregate', 'description', '{aggr_id}_aggr.csv')
    params:
        script = srcdir('scripts/convert_scanpy.py')
    output:
        join(QUANT_INTERIM, 'aggregate', '{quant}', 'velocyto', '{aggr_id}.h5ad')
    singularity:
        'docker://gcfntnu/scanpy:1.4.3'
    shell:
        'python {params.script} {input.loom} --aggr-csv {input.csv} -o {output} -v -f velocyto '

#-*- mode:snakemake -*-

extra_conf_fn = srcdir('star.config')
if os.path.exists(extra_conf_fn):
    with open(extra_conf_fn) as fh:
        c  = yaml.load(fh) or {}
        update_config2(config, c)

#config
ORG = config.get('organism', 'homo_sapiens')
STAR_PROCESSED = join(QUANT_PROCESSED, 'star')
STAR_INTERIM = join(QUANT_INTERIM, 'star')
STAR_CONF = config['quant'].get('star', {})


def starsolo_R1(wildcards):
    return ','.join(get_filtered_fastq_R1(wildcards))

def starsolo_R2(wildcards):
    return ','.join(get_filtered_fastq_R2(wildcards))

rule starsolo_quant:
    input:
        R1 = get_filtered_fastq_R1,
        R2 = get_filtered_fastq_R2,
        genome = join(REF_DIR, 'star', 'SA'),
        whitelist = join(EXT_DIR,'10xgenomics', 'whitelist.txt')
    params:
        outdir = join(STAR_INTERIM, '{sample}') + '/',
        genome_dir = join(REF_DIR, 'star'),
        cb_len = LIBPREP['starsolo']['cb_len'],
        umi_len = LIBPREP['starsolo']['umi_len'],
        R1 = starsolo_R1,
        R2 = starsolo_R2,
        extra_args = '--outSAMunmapped Within KeepPairs --outSAMorder PairedKeepInputOrder --genomeLoad LoadAndKeep --readFilesCommand zcat'
    threads:
        48
    output:
        barcodes = join(STAR_INTERIM, '{sample}', 'Solo.out', 'barcodes.tsv'),
        gene_stats = join(STAR_INTERIM, '{sample}', 'Solo.out', 'Gene.stats'),
        genes = join(STAR_INTERIM, '{sample}', 'Solo.out', 'genes.tsv'),
        feature_mtx = join(STAR_INTERIM, '{sample}', 'Solo.out', 'matrix.mtx')
    singularity:
        STAR_CONF['docker']
    benchmark:
        'benchmark/starsolo/{sample}-starsolo.txt'
    shell:
        'STAR '
        '--soloType Droplet '
        '--soloCBwhitelist {input.whitelist} '
        '--readFilesIn {params.R2} {params.R1} '
        '--genomeDir {params.genome_dir} '
        '--outFileNamePrefix {params.outdir} '
        '--soloCBlen {params.cb_len} '
        '--soloUMIlen {params.umi_len} '
        '--runThreadN {threads} '
        '{params.extra_args} '

rule starsolo_clean_shmem:
    params:
        genome_dir = rules.starsolo_quant.params.genome_dir
    output:
        temp(touch('.starsolo.mem.cleaned'))
    singularity:
        STAR_CONF['docker']    
    shell:
        'STAR --genomeDir {params.genome_dir} --genomeLoad Remove || echo "no shared mem"'

rule starsolo_scanpy_convert:
    input:
        mat = join(STAR_INTERIM, '{sample}', 'Solo.out', 'matrix.mtx'),
        mem_clean = '.starsolo.mem.cleaned'
    params:
        script = srcdir('scripts/cellranger_scanpy.py')
    output:
        join(STAR_INTERIM, '{sample}', 'scanpy', 'adata.h5ad')
    singularity:
        'docker://gcfntnu/scanpy:1.4'
    shell:
        'python {params.script} {input.mat} -f star -o {output} -v '
 
rule starsolo_scanpy_aggr:
    input:
        mat = expand(rules.starsolo_quant.output.feature_mtx, sample=SAMPLES),
        mem_clean = '.starsolo.mem.cleaned'
    params:
        script = srcdir('scripts/cellranger_scanpy.py'),
        norm = config['quant']['aggregate']['norm']
    output:
        join(QUANT_INTERIM, 'aggregate', 'star', 'scanpy', 'scanpy_aggr.h5ad')
    singularity:
        'docker://gcfntnu/scanpy:1.4'
    shell:
        'python {params.script} '
        '{input} '
        '-o {output} '
        '-f star '
        '--normalize {params.norm} '
        '-v '       

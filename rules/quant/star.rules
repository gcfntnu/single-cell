#-*- mode:snakemake -*-

extra_conf_fn = srcdir('star.config')
if os.path.exists(extra_conf_fn):
    with open(extra_conf_fn) as fh:
        c  = yaml.load(fh) or {}
        update_config2(config, c)

#config
ORG = config.get('organism', 'homo_sapiens')
STAR_PROCESSED = join(QUANT_PROCESSED, 'star')
STAR_INTERIM = join(QUANT_INTERIM, 'star')
STAR_CONF = config['quant'].get('star', {})


def starsolo_R1(wildcards):
    return ','.join(get_filtered_fastq_R1(wildcards))

def starsolo_R2(wildcards):
    return ','.join(get_filtered_fastq_R2(wildcards))

rule starsolo_quant:
    input:
        R1 = get_filtered_fastq_R1,
        R2 = get_filtered_fastq_R2,
        genome = join(REF_DIR, 'star', 'SA'),
        whitelist = rules.txgenomics_whitelist.output
    params:
        outdir = join(STAR_INTERIM, '{sample}') + '/',
        genome_dir = join(REF_DIR, 'star'),
        cb_len = STAR_CONF['cb_len'],
        umi_len = STAR_CONF['umi_len'],
        R1 = starsolo_R1,
        R2 = starsolo_R2
    threads:
        48
    output:
        barcodes = join(STAR_INTERIM, '{sample}', 'Solo.out', 'barcodes.tsv'),
        gene_stats = join(STAR_INTERIM, '{sample}', 'Solo.out', 'Gene.stats'),
        genes = join(STAR_INTERIM, '{sample}', 'Solo.out', 'genes.tsv'),
        feature_mtx = join(STAR_INTERIM, '{sample}', 'Solo.out', 'matrix.mtx'),
    singularity:
        STAR_CONF['docker']
    benchmark:
        'benchmark/starsolo/{sample}-starsolo.txt'
    shell:
        'STAR --soloType Droplet '
        '--soloCBwhitelist {input.whitelist} '
        '--readFilesIn {params.R2} {params.R1} '
        '--readFilesCommand zcat '
        '--outSAMunmapped Within KeepPairs '
        '--outSAMorder PairedKeepInputOrder '
        '--genomeDir {params.genome_dir} '
        '--outFileNamePrefix {params.outdir} '
        '--runThreadN {threads} '
        '--soloCBlen {params.cb_len} '
        '--soloUMIlen {params.umi_len} '
        '--genomeLoad LoadAndKeep '

rule starsolo_clean_shmem:
    params:
        genome_dir = rules.starsolo_quant.params.genome_dir
    output:
        temp(touch('.starsolo.mem.cleaned'))
    singularity:
        STAR_CONF['docker']    
    shell:
        'STAR --genomeDir {params.genome_dir} --genomeLoad Remove || echo "no shared mem"'

rule starsolo_scanpy:
    input:
        mat = join(STAR_INTERIM, '{sample}', 'Solo.out', 'matrix.mtx'),
        mem_clean = '.starsolo.mem.cleaned'
    params:
        script = srcdir('scripts/cellranger_scanpy.py')
    output:
        join(STAR_INTERIM, '{sample}', 'scanpy', 'adata.h5ad')
    singularity:
        'docker://gcfntnu/scanpy:1.4'
    shell:
        'python {params.script} {input.mat} -v -f star -o {output} '
 
